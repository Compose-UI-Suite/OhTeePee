plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def getVersionName = { ->
    return "0.0.2"
}

def getArtifactId = { ->
    return "ohteepee"
}

android {
    namespace 'com.composeuisuite.ohteepee'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.1'
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId 'com.composeuisuite'
            artifactId getArtifactId()
            version getVersionName()
            artifact("$buildDir/outputs/aar/${getArtifactId()}-release.aar")
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Compose-UI-Suite/OhTeePee")
            credentials {
                username = "tarikyasar"
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

dependencies {
    // Compose
    implementation "androidx.activity:activity-compose:$composeActivity"
    implementation "androidx.compose.ui:ui:$compose"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose"

    // Material
    implementation "androidx.compose.material:material:$material"
}